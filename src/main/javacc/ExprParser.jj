options
{
  STATIC = false;
  JAVA_UNICODE_ESCAPE = true;
}

PARSER_BEGIN(ExprParser)
package net.sourceforge.templat.expr;
import net.sourceforge.templat.parser.context.ContextStack;
import net.sourceforge.templat.exception.TemplateParsingException;

public class ExprParser
{
  private ExprActions actions;

  public Object parse(final ContextStack stackContext) throws ParseException, TemplateParsingException
  {
    this.actions = new ExprActions(stackContext);
    return Expression();
  }
}

PARSER_END(ExprParser)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
}

TOKEN :
{
  < DOT : "." >
| < COMMA : "," >
| < NUM : ([ "0"-"9" ])+ >
| < ID : ([ "A"-"Z", "a"-"z", "_" ]) ([ "A"-"Z", "a"-"z", "_", "0"-"9" ])* >
| < LBRACK : "[" >
| < RBRACK : "]" >
| < LPAREN : "(" >
| < RPAREN : ")" >
| < BANG : "!" >
}

Object Expression() throws TemplateParsingException :
{
  Object o;
  Object selectors = this.actions.createList();
}
{
  (
    < BANG > o = Expression()
    {
      return !(Boolean) o;
    }
  | < LPAREN > o = Expression() < RPAREN >
    {
      return o;
    }
  | o = < NUM >
    {
      return Integer.valueOf(o.toString());
    }
  | o = Name() selectors = Selectors()
    {
      return this.actions.applySelectors(o, selectors);
    }
  )
  < EOF >
}

Object Selectors() throws TemplateParsingException :
{
  Object selectors = this.actions.createList();
  Object selector;
}
{
  (
    selector = Selector()
    {
      selectors = this.actions.addToList(selector, selectors);
    }
  )*
  {
    return selectors;
  }
}

Object Selector() throws TemplateParsingException :
{
  Object method;
  Object argList;
  Object subscript;
}
{
  < DOT > method = < ID > < LPAREN > argList = ArgList() < RPAREN >
  {
    return this.actions.createMethodCall(method, argList);
  }
| < LBRACK > subscript = Expression() < RBRACK >
  {
    return this.actions.createArraySubscript(subscript);
  }
}

Object ArgList() throws TemplateParsingException :
{
  Object argList = this.actions.createList();
  Object expr;
}
{
  [
    expr = Expression()
    {
      argList = this.actions.addToList(expr, argList);
    }
    (
      < COMMA > expr = Expression()
      {
        argList = this.actions.addToList(expr, argList);
      }
    )*
  ]
  {
    return argList;
  }
}

Object Name() :
{
  Object name, id;
}
{
  name = < ID >
  (
    LOOKAHEAD(< DOT > < ID >, { !(getToken(1).kind == DOT && getToken(2).kind == ID && getToken(3).kind == LPAREN) })
    < DOT > id = < ID >
    {
      name = name + "." + id;
    }
  )*
  {
    return name;
  }
}
